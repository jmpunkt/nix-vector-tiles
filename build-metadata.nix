{
  runCommand,
  writeText,
  fetchFromGitHub,
  jq,
  yajsv,
}: {
  tileJson,
  tiles,
  attribution ? "<a href='https://www.openstreetmap.org/copyright/'>&#169; OSM contributors</a>",
}: let
  base-spec-v3 = writeText "app-config.json" (builtins.toJSON
    ({
        tilejson = "3.0.0";
        scheme = "xyz";
        version = "1.0.0";
        inherit attribution;
      }
      // tileJson));
  spec = fetchFromGitHub {
    owner = "mapbox";
    repo = "tilejson-spec";
    rev = "22f5f91e643e8980ef2656674bef84c2869fbe76";
    sha256 = "sha256-u9BkSScetwc+ebaHsiOVJgFmi3nWpUOS4Okia2zcpsA=";
  };
in
  runCommand "mapbox-vector-tiles" {
    buildInputs = [jq yajsv];
  } ''
    mkdir -p $out

    # extract and convert data from manifest.json generated by tilemaker
    jq -c '. |= {minzoom,maxzoom,bounds,center} * (.json | fromjson)
          | .minzoom |= tonumber
          | .maxzoom |= tonumber
          | .bounds |= split(",") | .bounds[] |= tonumber
          | .center |= split(",") | .center[] |= tonumber' ${tiles}/tiles/metadata.json > tmp.json
    # add missing fields to manifest
    jq -c '. * input' ${base-spec-v3} tmp.json > v3.json
    # verify that produced manifest is correct
    yajsv -s ${spec}/3.0.0/schema.json v3.json

    mv v3.json $out/
    ln -s ${tiles}/tiles $out
  ''
